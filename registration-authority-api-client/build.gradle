buildscript {
    ext {
        swaggerCoreVersion = '2.2.25'
        jacksonDatabindNullableVersion = '0.2.6'
    }
}

plugins {
    id "de.undercouch.download" version "5.3.0"
    id "org.openapi.generator" version "7.8.0"
    id 'org.springframework.boot' version "${springBootVersion}" apply false
    id 'io.spring.dependency-management' version "${springDependencyManagementPluginVersion}"
    id 'java-library'
}

dependencyManagement {
    imports {
        mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
    }
}

dependencies {
    // HTTP client: Spring RestTemplate
    api 'org.springframework:spring-web'
    api 'org.springframework:spring-context'
    api "io.swagger.core.v3:swagger-annotations:${swaggerCoreVersion}"
    // JSON processing
    api 'com.fasterxml.jackson.core:jackson-databind'
    api 'com.fasterxml.jackson.core:jackson-annotations'
    api 'com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider'
    api 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    api "org.openapitools:jackson-databind-nullable:${jacksonDatabindNullableVersion}"
    compileOnly 'jakarta.annotation:jakarta.annotation-api'
}

compileJava {
    dependsOn(tasks.openApiGenerate)
}

sourceSets {
    main {
        java {
            srcDir new File(buildDir as File, "generate-resources/main/")
        }
    }
}

task downloadFile(type: Download) {
    src "https://raw.githubusercontent.com/SwissSign-AG/RaApi/main/api.json"
    dest buildDir
    onlyIfModified true
    useETag false
}

openApiGenerate {
    inputSpec = "$buildDir/api.json"
    apiPackage = "ch.swisssign.ra.gen.client.api"
    invokerPackage = "ch.swisssign.ra.gen.client.invoker"
    modelPackage = "ch.swisssign.ra.gen.client.model"
    generateApiTests = false
    generateModelTests = false
    modelNameSuffix = "Dto"
    generatorName = "java"
    library = "resttemplate"
    configOptions = [
            dateLibrary: "java8",
            useJakartaEe: "true"
    ]
    skipValidateSpec = false
    logToStderr = true
    generateAliasAsModel = false
    enablePostProcessFile = false
}

tasks.openApiGenerate.dependsOn tasks.downloadFile
